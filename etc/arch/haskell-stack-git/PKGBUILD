# Maintainer:  Tristan Webb <tristan@fpcomplete.com>
pkgname=haskell-stack-git
_pkgname=stack
pkgver=20150616.8b8eeea
pkgrel=1
pkgdesc="The Haskell Tool Stack"
arch=('i686' 'x86_64')
url="https://www.github.com/commercialhaskell/stack"
license=('BSD3')
depends=('libtinfo')
makedepends=('ghc' 'cabal-install')
optdepends=('docker: Use Docker images to build your project in a temporary container')
provides=('haskell-stack')
conflicts=('haskell-stack')
# install=
source=("${_pkgname}::git+https://github.com/commercialhaskell/stack.git")
md5sums=('SKIP')

prepare() {
  cd "$srcdir/${_pkgname}"
  rm -rf .cabal-sandbox
  cabal sandbox init
  rm -f cabal.config
  wget https://www.stackage.org/nightly/cabal.config
  cabal update
  cabal clean
  cabal install --only-dependencies --reorder-goals --enable-tests
  cabal install cpphs
}

build() {
  cd "$srcdir/${_pkgname}"
  BIN_DIR="$srcdir/${_pkgname}/bin"
  SHARE_DIR="$srcdir/${_pkgname}/share"
  mkdir -p $BIN_DIR
  mkdir -p $SHARE_DIR
  cabal install --disable-documentation \
        --disable-library-profiling \
        --bindir=$BIN_DIR
}

check() {
  cd "$srcdir/${_pkgname}"
  cabal test
}

package() {
  cd "$srcdir/${_pkgname}"
  BIN_DIR="$srcdir/${_pkgname}/bin/"
  STACK_BIN=$BIN_DIR/stack
  install -Dm755 $STACK_BIN "$pkgdir/usr/bin/stack"
}

pkgver() {
cd "${srcdir}/${_pkgname}"
  git log -1 --format='%cd.%h' --date=short | tr -d -
}

# vim:set ts=2 sw=2 et:
